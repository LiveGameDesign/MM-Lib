
Base //base definition
{
  pool self at 1 //self represents this instance
  in ref creeps  //input reference: creeps in the world
  in ref essence //input reference: essence in the world
  out ref gold   //output reference: gold the player has
  
  user converter hitByCreep   //Physics creep collision
  auto /*user*/ converter hitByEssence //Physics essence collision
  
  die:     creeps --> hitByCreep //when a creep collides
  destroy: self --> hitByCreep   //this base is destroyed
  
  absorb:  essence --> hitByEssence //when essence hits
  cash:    hitByEssence --> gold    //gold is generated
}

Tower //tower definition
{
  pool self at 1   //self represents this instance
  in ref creeps    //input reference: creeps in the world
  out ref essence  //output reference: essence in the world
  
  //note:  specify physics units of measurements here
  pool range at 150        //tower range is 150
  pool firePower at 50     //tower fire power is 50
  pool rotationSpeed at 20 //tower rotation speed is 20
  
  user converter hitByCreep //Physics creep collision
  user converter killCreep  //Physics creep kill
  
  collapse: self --> hitByCreep //collapsing this tower
  burry: creeps --> hitByCreep  //burries a creep
  
  die: creeps --> killCreep           //killing a kreep
  gainEssence: killCreep -5-> essence //generates 5 essence
}

auto /*user*/ source spawn //Physics activates spawn source
pool creeps                //creeps in the world
create: spawn --> creeps   //spawn creates one creep per step

pool essence             //essence in the world
user drain missed        //Physics activates missed essence
miss: essence --> missed //drain one essence per miss

pool gold at 120         //player gold starts at 120
pool towers of Tower     //number of towers in the world
pool bases of Base       //number of bases in the world
auto /*user*/ converter buyTower  //User activates buyTower
auto /*user*/ converter buyBase   //User activates buyBase
buyT: gold -20-> buyTower      //A tower costs 20 gold
buyB: gold -50-> buyBase       //A base costs 50 gold
getT: buyTower --> towers
getB: buyBase --> bases

//Base bindings
baseEssence: essence .=.> bases.essence
baseCreeps: creeps .=.> bases.creeps
baseGold: gold .=.> bases.gold

//Tower bindings
towerEssence: essence .=.> towers.essence
towerGold: creeps .=.> towers.creeps

assert lose : gold < 50 && bases == 0 "you lose"
assert win  : bases >= 20             "you win"

modify